label ep001_wakeup:
    scene my_room with eyeopen
    pause(0.2)
    scene black with eyeclose
    pause(1.0)
    scene my_room with eyeopen
    
    "{i}Маленькая квартирка на окраине города пропахла дымом сигарет и квашенной капустой должно быть еще со времен прошлого владельца. {/i}"
    '{i}Из окна открывается "восхитительный" вид на промышленный район.{/i}'
    $ global_time.show_time()
    $ my_job_timemark = Time_mark(global_time, 0, 2, 10) #на работу к 9
    #$ t = my_job_timemark.get_time()
    #"на работу к [t]"
    "Это серое утро серого города... "        
    "Вчера снова приходили коллекторы. Они с удовольствием забрали бы у меня все, что есть и оставили подыхать, если бы у меня что-то еще осталось"
    "Достану я эти чертовы деньги!"
    "Они забрали все, что было, и дали мне десять дней, чтобы собрать всю оставшуюся сумму." 
    "Я должен 10 000$."
    #pause(1.0)
    "Чувствую себя так, словно меня переехали автомобилем. Все тело разбито."
    "Джонни говорит, что я совсем себя не берегу. Было бы что беречь..."
    "Нужно проснуться, раскрыть глаза и встать. Чертова работа..."
    "Но если меня уволят, это будет проблема."

    #"На моем счету [stat_money]$. Нужно больше. Значительно больше."
    #"Они дали мне 10 дней, мне нужно где-то достать 10 000$"
    
    

    menu:
        "Встать и одеться":
            $ global_time.add_time("m", 5)
            $ change_stat("r", 1)            
        "Поспать еще немного":
            $ pospal = renpy.random.randint(20, 80)
            $ global_time.add_time("m", pospal) 
            $ change_stat("md", 8) 
            "Поворачиваюсь на другой бок и закрываю глаза"
            scene black with eyeclose
            ".................................................................."
            scene my_room with eyeopen
            show me_big at left
            with dissolve
            me "Вот так намного лучше!"

    show me_big at left
    with fade

    # поменять сравнение времени
    $ global_time.show_time()
    if (global_time.hours_actual > 7) or (global_time.hours_actual == 7 and global_time.minutes_actual > 30):
        "Вот дьявол! Опаздываю!"
        "Мне нельзя опаздывать! Нельзя терять работу! Только не сейчас!"
        $ change_stat("md", -5)
    else:
        "Так. Сейчас [global_time.hours_actual]-[global_time.minutes_actual]. К 9 мне нужно добраться до места, дорога займет около полутора часов"
        if global_time.hours_actual == 7 and global_time.minutes_actual < 20:        
            "Что ж, у меня есть немного времени"
        elif global_time.hours_actual == 6:
            "У меня достаточно времени, чтобы все успеть"
            $ change_stat("md", 3)

        menu:
            "Выпить кофе и позавтракать":
                $ change_stat("md", 10)
                $ change_stat("r", 1)
                $ global_time.add_time("m", 30)
                "Я варю горячий кофе и жарю яичницу с беконом."
                "Этот горячий ароматный кофе невероятно бодрит"
                $ renpy.notify("Ваше состояние улучшилось, в голове проясняется.")
            "Выпить виски":
                $ change_stat("md", -20)
                $ change_stat("h", -8)
                "Надо чем-то скрасить это мерзкое утро."
                "Достаю из шкафа бутылку и плескаю немного в стакан. Засыпаю льдом. Перемешиваю."
                "Вообще мне не следует употреблять, могут быть проблемы."
                "Но этот день определенно становится лучше"
                $ renpy.notify("Вы не очень хорошо себя чувствуете")
                $ global_time.add_time("m", 30)
                $ my_choises["001_visky_drink"] = True
            "Ничего не пить":
                $ global_time.add_time("m", 5)
                "Лучше пораньше выйти из дома"
    pause(2.0)
    

    "Так, проверим заначку. Хотя бы ее не тронули."
    "Я отгибаю старую половицу и проверяю тайник."

    $ count = room1_secret.how_mutch("money")
    "[count]$... не густо, но хоть что-то"
    menu:
        "Забрать":
            "Заберу с собой. Они мне понадобятся."
            $ room1_secret.take_money()       

        "Оставить":
            "Пусть полежат здесь. Так они будут в большей безопасности, чем в моем кармане."

    "Рядом лежит небольшой старинный кинжал мастерской работы"
    menu:
        "Взять с собой":
            "Заберу это с собой. В свете последних событий неплохо иметь при себе оружие."
            $ room1_secret.give_away(my_inventory, "myDagger")
        "Оставить":
            "Пожалуй, оставлю это здесь."

    "Хорошо, что у меня есть тайник."
    pause(1.0)
    
    "Выхожу в прихожую и надеваю ботинки."
    "Вдруг..."
    "Что это?"            
    "Что-то мешается в ботинке"
    "Достаю клочек бумаги"

    show screen letter("Поставь 15$ на 36. Сегодня тебе повезет", "me")
    $ timer_luch_36 = Time_mark(global_time)
    $ roulette_luck_36 = True

    "Что за черт?!"
    "Я завязал!"
    "Но кто мог подбросить мне эту записку?!"

    $ global_time.add_time("m", 10)
    $ global_time.show_time()
    "Мне пора, потом разберусь."
    "Прячу записку в карман"

    scene bg london with fade
    show rain

    "Этот дождь никогда не заканчивается. День и ночь почти неразличимы, и сменяются один другим в бесконечной череде."
    "Вся моя жизнь - гусеничная карусель этих невыносимых единообразных будней и ярких вспышек веселья, когда я ставлю все на кон."
    "Я знаю этот город, каждая клетка моего тела пропитана им. Серый туман, яркие огни казино и борделей. Все оттенки соблазнов, пороков, богатства и нищеты"

    $ global_time.add_time("m", 40)

    label .go_menu:
        menu:
            "Пойти на работу":            
                jump ep001_gowork

            "Зайти в казино":
                call go_casino from _call_go_casino
                jump .go_menu

            "Пойти куда-то еще":
                #[] здесь должны быть другие варианты
                "На работу, сцуко иддди!"
                jump ep001_gowork
                        

    return


label ep001_gowork:
    $ global_time.add_time("m", renpy.random.randint(35, 55)) #время на оставшуюся дорогу до работы
    
    menu:
        "Я работаю в "
        "Магазине":
            $ my_job = "shop"
            #$ renpy.notify("Магазин закрыт, так что ты работаешь в морге!")

        "Морге":
            $ my_job = "morg"

    menu:
        "Мой начальник"
        "отличный парень":
            $ my_boss_kind = boss_kind["good"]

        "настоящий козлина":
            $ my_boss_kind = boss_kind["bad"]   

    call init_boss from _call_init_boss

    #XX здесь будет проверка, на какой работе ты работаешь
    if my_job == "morg":
        call morg_first_day from _call_morg_first_day     
    elif my_job == "shop":
        call shop_first_day from _call_shop_first_day
    
    scene bg police with fade
    #show rain with dissolve
    $ global_time.show_time()

    "Выхожу на улицу. Наконец-то свежий воздух этого славного города! И дождь закончился."
    "Нужно прогуляться"
    label ep001_wolk_menu:
        menu:            
            "Пойти в парк":
                "Пойду в парк"
            "Пойти в казино":
                "Пойду в казино."
                call go_casino from _call_go_casino_1
                jump ep001_wolk_menu
            "Пойти в ломбард":
                "Схожу в ломбард"          
            #"Пойти в бордель": 
            "Пойти домой":
                "Пойду домой. Хватит на сегодня развлечений."     
        
return                

