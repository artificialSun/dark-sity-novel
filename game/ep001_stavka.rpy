label ep001_stavka:
    scene my_room 
    
    "{i}Маленькая квартирка на окраине города пропахла дымом сигарет и квашенной капустой должно быть еще со времен прошлого владельца. {/i}"
    '{i}Из окна открывается "восхитительный" вид на промышленный район.{/i}'
    $ show_time()
    "Это серое утро серого города... "        
    "Вчера снова приходили коллекторы. Они с удовольствием забрали бы у меня все, что есть и оставили подыхать, если бы у меня что-то еще осталось"
    "Достану я эти чертовы деньги!"
    "Чувствую себя так, словно меня переехали автомобилем. Все тело разбито."
    "Джонни говорит, что я совсем себя не берегу. Было бы что беречь..."
    "Нужно проснуться, раскрыть глаза и встать. Чертова работа..."
    "Но если меня уволят, это будет проблема."

    #"На моем счету [stat_money]$. Нужно больше. Значительно больше."
    #"Они дали мне 10 дней, мне нужно где-то достать 10 000$"
    

    menu:
        "Встать и одеться":
            $ add_time("m", 5)
            call change_stat("r", 1)            
        "Поспать еще немного":
            $ add_time("m", renpy.random.randint(20, 80)) 
            $ change_stat("md", 8) 
            "Поворачиваюсь на другой бок и закрываю глаза"
            ".................................................................."
            show me_big at left
            with fade
            me "Вот так намного лучше!"

    show me_big at left
    with fade

    $ show_time()
    if (hours_actual > 7) or (hours_actual == 7 and minutes_actual > 30):
        "Вот дьявол! Опаздываю!"
        "Мне нельзя опаздывать! Нельзя терять работу! Только не сейчас!"
        $ change_stat("md", -5)
    else:
        "Так. Сейчас [hours_actual]-[minutes_actual]. К 9 мне нужно добраться до места, дорога займет около полутора часов"
        if hour_start == 7 and minutes_actual < 20:        
            "Что ж, у меня есть немного времени"
        elif hour_start == 6:
            "У меня достаточно времени, чтобы все успеть"
            $ change_stat("md", 3)
            #block of code to run
        menu:
            "Выпить кофе и позавтракать":
                $ change_stat("md", 10)
                $ change_stat("r", 1)
                $ add_time("m", 30)
                "Я варю горячий кофе и жарю яичницу с беконом."
                "Этот горячий ароматный кофе невероятно бодрит"
                $ renpy.notify("Ваше состояние улучшилось, в голове проясняется.")
            "Выпить виски":
                $ change_stat("md", -20)
                $ change_stat("h", -8)
                "Надо чем-то скрасить это мерзкое утро."
                "Достаю из шкафа бутылку и плескаю немного в стакан. Засыпаю льдом. Перемешиваю."
                "Вообще мне не следует употреблять, могут быть проблемы."
                "Но этот день определенно становится лучше"
                $ renpy.notify("Вы не очень хорошо себя чувствуете")
                $ add_time("m", 30)
            "Ничего не пить":
                $ add_time("m", 5)
                "Лучше пораньше выйти из дома"
    pause(2.0)
    "Они забрали все, что было, и дали мне десять дней, чтобы собрать всю оставшуюся сумму." 
    "Я должен 10 000$."
    pause(1.0)

    "Так, проверим заначку."
    "Я отгибаю старую половицу и проверяю тайник."
    #"[room1_secret_money]$... не густо, но хоть что-то"    
    #$ room1_secret.show_all()

    $ count = room1_secret.how_mutch("money")
    "[count]$... не густо, но хоть что-то"
    menu:
        "Забрать":
            "Заберу с собой. Они мне понадобятся."
            $ room1_secret.take_money()
            #$ change_stat("m", room1_secret_money)
            #$ room1_secret_money = 0            

        "Оставить":
            "Пусть полежат здесь. Так они будут в большей безопасности, чем в моем кармане."

    "Рядом лежит небольшой старинный кинжал мастерской работы"
    $ room1_secret.show_all()
    menu:
        "Взять с собой":
            "Заберу это с собой. В свете последних событий неплохо иметь при себе оружие."
            #$ my_inventory.append("старинный кинжал")
            #$ room1_secret.remove("старинный кинжал")
            #$ renpy.notify("старинный кинжал добавлен в ваш инвентарь")
            $ room1_secret.give_away(my_inventory, "myDagger")
        "Оставить":
            "Пожалуй, оставлю это здесь."

    "Хорошо, что у меня есть тайник."
    pause(1.0)
    
    "Выхожу в прихожую и надеваю ботинки."
    "Вдруг..."
    "Что это?"            
    "Что-то мешается в ботинке"
    "Достаю клочек бумаги"

    show screen letter("Поставь 15$ на 36. Сегодня тебе повезет", "me")

    #здесь должен быть экран с запиской вместо текста, а еще она должна сохраняться в журнал или инвентарь для просмотра
    "{i}Поставь 15$ на 36. Сегодня тебе повезет{/i}"    

    "Что за черт?!"
    "Я завязал!"
    "Но кто мог подбросить мне эту записку?!"

    $ add_time("m", 10)
    $ show_time()
    me "Мне пора, потом разберусь."
    "Прячу записку в карман"

    scene bg london with fade
    show rain

    "Этот дождь никогда не заканчивается. День и ночь почти неразличимы, и сменяются один другим в бесконечной череде."
    "Вся моя жизнь - гусеничная карусель этих невыносимых единообразных будней и ярких вспышек веселья, когда я играю в шик и ставлю все на кон."
    "Я знаю этот город, каждая клетка моего тела пропитана им. Серый туман, яркие огни казино и борделей"
    



    $ add_time("m", 5)
    $ show_time()

    


    return
